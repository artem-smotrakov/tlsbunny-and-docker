plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id 'org.sonarqube' version '2.6.2'
}

version '1.0-SNAPSHOT'
sourceCompatibility = 1.9
mainClassName = "com.gypsyengineer.tlsbunny.Main"

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

jacoco {
    toolVersion = '0.8.2'
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/core'
        }
    }

    vendor {
        java {
            srcDir 'src/vendor/java'
        }
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }

    test {
        java {
            srcDirs 'src/test/core', 'src/vendor/java'
        }

        compileClasspath += sourceSets.main.output
        compileClasspath += sourceSets.vendor.output
        compileClasspath += configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

test {
    if (project.hasProperty('tlsbunny.test.exclude')) {
        exclude project.property('tlsbunny.test.exclude')
    }

    if (project.hasProperty('tlsbunny.test.include')) {
        include project.property('tlsbunny.test.include')
    }

    systemProperty 'tlsbunny.output.standard.level',
            System.getProperty('tlsbunny.output.standard.level', 'important')

    systemProperty 'tlsbunny.sync.output.standard.level',
            System.getProperty('tlsbunny.sync.output.standard.level', 'important')

    systemProperty 'tlsbunny.output.enable.highlighting',
            System.getProperty('tlsbunny.output.enable.highlighting', 'true')
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

// build all docker images
task buildAllDocker() {
    dependsOn 'buildGnutlsDocker'
    dependsOn 'buildPicotlsDocker'
    dependsOn 'buildOpensslDocker'
    dependsOn 'buildH2ODocker'
    dependsOn 'buildNssDocker'
    dependsOn 'buildWolfsslDocker'
}

// check for crashes
task checkCrashes(type: Exec) {
    commandLine 'bash', 'scripts/check_logs_for_crashes.sh',
            'gnutls_server_tls13',
            'nss_server_tls13',
            'h2o_server_tls13',
            'picotls_server_tls13',
            'openssl_server_tls13',
            'wolfssl_server_tls13'
}

// tasks for running tests for GnuTLS

task buildGnutlsDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--no-cache',
            '--file', 'src/main/docker/gnutls/Dockerfile',
            '--tag',  'gnutls/server/tls13',
            '.'
}

// tasks for running tests for OpenSSL

task buildOpensslDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--no-cache',
            '--file', 'src/vendor/docker/openssl/Dockerfile',
            '--tag',  'artemsmotrakov/tlsbunny_openssl_tls13',
            '.'
}

task runOpensslTests(type: Test) {
    description = 'Runs tests for OpenSSL'
    group = 'openssl'

    testClassesDirs += sourceSets.vendor.output.classesDirs
    testClassesDirs += sourceSets.test.output.classesDirs
    classpath += sourceSets.vendor.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    testLogging.showStandardStreams = true
    ignoreFailures = true
    outputs.upToDateWhen { false }

    include 'com/gypsyengineer/tlsbunny/vendor/test/tls13/openssl/**/*Tests*'
    exclude 'com/gypsyengineer/tlsbunny/vendor/test/tls13/openssl/**/*Fuzzing*'
}

task runOpensslFuzzing(type: Test) {
    description = 'Runs fuzzing for OpenSSL'
    group = 'openssl'

    testClassesDirs += sourceSets.vendor.output.classesDirs
    testClassesDirs += sourceSets.test.output.classesDirs
    classpath += sourceSets.vendor.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    testLogging.showStandardStreams = true
    ignoreFailures = true
    outputs.upToDateWhen { false }

    include 'com/gypsyengineer/tlsbunny/vendor/test/tls13/openssl/**/*Fuzzing*'
}

// tasks for running tests for picotls

task buildPicotlsDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--no-cache',
            '--file', 'src/main/docker/picotls/Dockerfile',
            '--tag',  'picotls/server/tls13',
            '.'
}

// tasks for running tests for h2o + picotls

task buildH2ODocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--no-cache',
            '--file', 'src/main/docker/h2o/Dockerfile',
            '--tag',  'h2o/server/tls13',
            '.'
}

// tasks for running tests for NSS

// builds a Docker images with NSS server
task buildNssDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--no-cache',
            '--file', 'src/main/docker/nss/Dockerfile',
            '--tag',  'nss/server/tls13',
            '.'
}

// tasks for running tests for WolfSSL

// builds a Docker images with WolfSSL server
task buildWolfsslDocker(type: Exec) {
    commandLine 'docker',
            'build',
            '--no-cache',
            '--file', 'src/vendor/docker/wolfssl/Dockerfile',
            '--tag',  'artemsmotrakov/tlsbunny_wolfssl_tls13',
            '.'
}

task runWolfsslTests(type: Test) {
    description = 'Runs tests for WolfSSL'
    group = 'wolfssl'

    testClassesDirs += sourceSets.vendor.output.classesDirs
    testClassesDirs += sourceSets.test.output.classesDirs
    classpath += sourceSets.vendor.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    testLogging.showStandardStreams = true
    ignoreFailures = true
    outputs.upToDateWhen { false }

    systemProperty 'tlsbunny.output.enable.highlighting', 'false'

    include 'com/gypsyengineer/tlsbunny/vendor/test/tls13/wolfssl/**/*Tests*'
    exclude 'com/gypsyengineer/tlsbunny/vendor/test/tls13/wolfssl/**/*Fuzzing*'
}

task runWolfsslFuzzing(type: Test) {
    description = 'Runs fuzzing for WolfSSL'
    group = 'wolfssl'

    testClassesDirs += sourceSets.vendor.output.classesDirs
    testClassesDirs += sourceSets.test.output.classesDirs
    classpath += sourceSets.vendor.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    testLogging.showStandardStreams = true
    ignoreFailures = true
    outputs.upToDateWhen { false }

    systemProperty 'tlsbunny.fuzzer.full.configs', 'true'
    systemProperty 'tlsbunny.output.to.file', 'true'
    systemProperty 'tlsbunny.output.enable.highlighting', 'false'
    systemProperty 'tlsbunny.output.standard.level', 'important'
    systemProperty 'tlsbunny.sync.output.standard.level', 'info'

    include 'com/gypsyengineer/tlsbunny/vendor/test/tls13/wolfssl/**/*Fuzzing*'
}

task runDeepWolfsslServerFuzzing(type: Test) {
    description = 'Runs deep fuzzing for WolfSSL server'
    group = 'wolfssl'

    testClassesDirs += sourceSets.vendor.output.classesDirs
    testClassesDirs += sourceSets.test.output.classesDirs
    classpath += sourceSets.vendor.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    testLogging.showStandardStreams = true
    ignoreFailures = true
    outputs.upToDateWhen { false }

    systemProperty 'tlsbunny.fuzzer.full.configs', 'true'
    systemProperty 'tlsbunny.output.to.file', 'true'
    systemProperty 'tlsbunny.output.enable.highlighting', 'false'
    systemProperty 'tlsbunny.output.standard.level', 'important'
    systemProperty 'tlsbunny.sync.output.standard.level', 'info'

    filter.includeTestsMatching 'com.gypsyengineer.tlsbunny.vendor.test.tls13.wolfssl.server.FuzzingForWolfsslServer.deepHandshakeFuzzer'
}

task runDeepOpensslServerFuzzing(type: Test) {
    description = 'Runs deep fuzzing for OpenSSL server'
    group = 'wolfssl'

    testClassesDirs += sourceSets.vendor.output.classesDirs
    testClassesDirs += sourceSets.test.output.classesDirs
    classpath += sourceSets.vendor.runtimeClasspath
    classpath += sourceSets.test.runtimeClasspath

    testLogging.showStandardStreams = true
    ignoreFailures = true
    outputs.upToDateWhen { false }

    systemProperty 'tlsbunny.fuzzer.full.configs', 'true'
    systemProperty 'tlsbunny.output.to.file', 'true'
    systemProperty 'tlsbunny.output.enable.highlighting', 'false'

    filter.includeTestsMatching 'com.gypsyengineer.tlsbunny.vendor.test.tls13.openssl.server.FuzzingForOpensslServer.deepHandshakeFuzzer'
    filter.includeTestsMatching 'com.gypsyengineer.tlsbunny.vendor.test.tls13.openssl.server.FuzzingForOpensslServerWithClientAuth.deepHandshakeFuzzer'
}